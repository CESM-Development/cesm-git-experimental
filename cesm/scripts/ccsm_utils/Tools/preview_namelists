#! /bin/csh -f

setenv EXEROOT		 `./xmlquery  EXEROOT		-value`
setenv LIBROOT		 `./xmlquery  LIBROOT		-value`
setenv INCROOT		 `./xmlquery  INCROOT		-value`
setenv RUNDIR		 `./xmlquery  RUNDIR	 	-value`
setenv SHAREDLIBROOT	 `./xmlquery  SHAREDLIBROOT	-value`
setenv CASEROOT		 `./xmlquery  CASEROOT		-value`
setenv CASEBUILD	 `./xmlquery  CASEBUILD		-value`
setenv COMP_CPL		 `./xmlquery  COMP_CPL		-value `
setenv COMP_ATM		 `./xmlquery  COMP_ATM		-value `
setenv COMP_LND		 `./xmlquery  COMP_LND		-value `
setenv COMP_ICE		 `./xmlquery  COMP_ICE		-value `
setenv COMP_OCN		 `./xmlquery  COMP_OCN		-value `
setenv COMP_GLC		 `./xmlquery  COMP_GLC		-value `
setenv COMP_WAV		 `./xmlquery  COMP_WAV		-value `
setenv COMP_ROF		 `./xmlquery  COMP_ROF		-value `

set MODELS     = (         cpl         atm         lnd         ice         ocn         glc         wav          rof)	  
set COMPONENTS = (   $COMP_CPL   $COMP_ATM   $COMP_LND   $COMP_ICE   $COMP_OCN   $COMP_GLC   $COMP_WAV    $COMP_ROF)  

if !($?LID) then
  setenv LID "`date +%y%m%d-%H%M%S`"
endif

unsetenv PREVIEW_NML
if ($argv =~ *verbose*) then
  setenv PREVIEW_NML 1
endif

# -------------------------------------------------------------------------
# Make necessary directories
# -------------------------------------------------------------------------

foreach dir ( $EXEROOT $LIBROOT $INCROOT $RUNDIR $SHAREDLIBROOT )
  if !(-d $dir) mkdir -p $dir || "cannot make $dir" && exit -1
end

foreach model ($MODELS)
  set objdir = $EXEROOT/$model/obj ; if !(-d $objdir) mkdir -p $objdir
  set libdir = $EXEROOT/$model     ; if !(-d $libdir) mkdir -p $libdir
end

# -------------------------------------------------------------------------
# Build Namelist
# -------------------------------------------------------------------------

@ n = 0
foreach model ($MODELS)
  @ n = $n + 1
  set comp = $COMPONENTS[$n]
  $CASEBUILD/$comp.buildnml $CASEROOT
  if ($status != 0) then
    echo  ERROR: $comp.buildnml failed; exit  99
  endif
end

# -------------------------------------------------------------------------
# Save namelist to docdir
# -------------------------------------------------------------------------

set docdir = $CASEROOT/CaseDocs
if !(-d $docdir) then
  mkdir -p $docdir
  echo "  CESM Resolved Namelist Files" >& $docdir/README
  echo "    For documentation only" >>& $docdir/README
  echo "    DO NOT MODIFY" >>& $docdir/README
endif
chmod +w $docdir/*

cd $RUNDIR
cp -p *_in_[0-9]*               $docdir/ >& /dev/null
cp -p *modelio*nml_[0-9]*	$docdir/ >& /dev/null
cp -p *_in                      $docdir/ >& /dev/null
cp -p *modelio*nml              $docdir/ >& /dev/null
cp -p *streams*txt*             $docdir/ >& /dev/null
cp -p *.stxt                    $docdir/ >& /dev/null
cp -p *maps.rc                  $docdir/ >& /dev/null
cp -p cism.config*              $docdir/ >& /dev/null

# copy over chemistry mechanism docs if they exist
if ( -e $CASEBUILD/camconf ) then
  cp -p $CASEBUILD/camconf/chem_mech.* $docdir/ >& /dev/null
endif
chmod 444 $docdir/*




